# Load configuration
configfile: "config/config.yaml"

# Include rule files
include: "rules/preprocessing.smk"
include: "rules/popdata.smk"
include: "rules/faststructure.smk"
include: "rules/admixture.smk"
include: "rules/pcaone.smk"
include: "rules/vcf2pcacluster.smk"
include: "rules/relatedness.smk"
include: "rules/pixy.smk"
include: "rules/structure.smk"


def get_all_targets():
    """
    Build list of target files based on enabled analyses in config.
    This keeps the main Snakefile clean and makes it easy to 
    enable/disable analyses by editing the config file.
    """
    targets = []
    analyses = config.get("analyses", {})
    PCA_groupings = config["pca_plot"]["color_by"]


    
    # FastStructure targets
    if analyses.get("faststructure", False):
        targets.extend([
            expand(config["analysis_name"] + "/faststructure/faststructure.{k}.meanQ", 
                   k=config["k_values"]),
            expand(config["analysis_name"] + "/faststructure/faststructure.{k}.meanP", 
                   k=config["k_values"]),
            config["analysis_name"] + "/faststructure/chooseK_results.txt"
        ])
    
    # ADMIXTURE targets
    if analyses.get("admixture", False):
        targets.extend([
            expand(config["analysis_name"] + "/admixture/biallelic_snps_thinned.{k}.Q", 
                   k=config["k_values"]),
            expand(config["analysis_name"] + "/admixture/biallelic_snps_thinned.{k}.P", 
                   k=config["k_values"]),
            config["analysis_name"] + "/admixture/chooseK_results.txt"
        ])
    
    # STRUCTURE targets
    if analyses.get("structure", False):
        n_reps = config["structure"].get("replicates", 1)
        targets.extend([
            expand(config["analysis_name"] + "/structure/structure.K{k}.R{r}_f", 
                   k=config["k_values"], 
                   r=range(1, n_reps + 1)),
            expand(config["analysis_name"] + "/structure/plots/K{k}_aligned.pdf", 
                   k=config["k_values"])

        ])
    
    # PCAone targets (standard)
    if analyses.get("pcaone", False):
        targets.extend([
            config["analysis_name"] + "/pcaone/PCA.eigvecs",
            config["analysis_name"] + "/pcaone/PCA.eigvals",
            expand("{analysis}/final_plots/PCA-{color_by}.pdf",
                analysis=config["analysis_name"],
                color_by=PCA_groupings)
        ])

    # PCAone targets (EMU)
    if analyses.get("pcaone_emu", False):
        targets.extend([
            config["analysis_name"] + "/pcaone_EMU/PCA_EMU.eigvecs",
            config["analysis_name"] + "/pcaone_EMU/PCA_EMU.eigvals"
        ])

    # PCAone targets (with coverage thresholds)
    if analyses.get("pcaone_miss", False):
        targets.extend([
            expand(config["analysis_name"] + "/pcaone_miss{miss}/PCA_miss{miss}.eigvecs", 
                   miss=config["PCAone"].get("miss", 0.5)),
            expand(config["analysis_name"] + "/pcaone_miss{miss}/PCA_miss{miss}.eigvals", 
                   miss=config["PCAone"].get("miss", 0.5))
        ])
    
    # VCF2PCACluster targets
    if analyses.get("vcf2pcacluster", False):
        miss_values = config["vcf2pcacluster"]["SNP_filtering"].get("Miss", [0.5])
        maf_values = config["vcf2pcacluster"]["SNP_filtering"].get("MAF", [0])
        
        targets.extend(
            expand(
                config["analysis_name"] + "/vcf2pcacluster_miss{miss}_MAF{MAF}/vcf2pcacluster_miss{miss}_MAF{MAF}.eigenvec",
                miss=miss_values,
                MAF=maf_values
            )
        )
        targets.extend(
            expand(
                config["analysis_name"] + "/vcf2pcacluster_miss{miss}_MAF{MAF}/vcf2pcacluster_miss{miss}_MAF{MAF}.eigenval",
                miss=miss_values,
                MAF=maf_values
            )
        )
    
    # Pixy targets
    if analyses.get("pixy", False):
        targets.extend([
            config["analysis_name"] + "/pixy/pixy_pi-summary.txt",
            config["analysis_name"] + "/pixy/pixy_fst-summary.txt",
            config["analysis_name"] + "/pixy/pixy_dxy-summary.txt"
        ])
    
    # Relatedness targets
    if analyses.get("relatedness", False):
        targets.extend([
            config["analysis_name"] + "/relatedness/out.relatedness",
            config["analysis_name"] + "/relatedness/out.relatedness2"
        ])
    
    # General targets
    targets.extend ([config["analysis_name"] + "/popdata.txt"])

    return targets

# Main target rule
rule all:
    input:
        get_all_targets()