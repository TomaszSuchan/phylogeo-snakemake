# Load configuration
configfile: "config/config.yaml"

# Include rule files
include: "rules/preprocessing.smk"
include: "rules/popdata.smk"
include: "rules/faststructure.smk"
include: "rules/admixture.smk"
include: "rules/pcaone.smk"
include: "rules/vcf2pcacluster.smk"
include: "rules/relatedness.smk"
include: "rules/pixy.smk"
include: "rules/structure.smk"
include: "rules/iqtree.smk"
include: "rules/mapmixture.smk"

def get_all_targets():
    """
    Build list of target files based on enabled analyses in config.
    This keeps the main Snakefile clean and makes it easy to 
    enable/disable analyses by editing the config file.
    Supports multiple projects, each with their own analyses.
    """
    targets = []
    
    # Iterate through each project
    for project in config["projects"].keys():
        project_config = config["projects"][project]
        analyses = project_config.get("analyses", {})
        params = project_config.get("parameters", {})
        
        # Get project-specific parameters
        k_values = params.get("k_values", [1, 2, 3])
        n_reps = params.get("structure", {}).get("replicates", 1)
        PCA_groupings = params.get("pca_plot", {}).get("color_by", ["Population"])
        
        # FastStructure targets
        if analyses.get("faststructure", False):
            targets.extend([
                expand("results/" + project + "/faststructure/" + project + ".faststructure.{k}.meanQ",
                       k=k_values),
                expand("results/" + project + "/faststructure/" + project + ".faststructure.{k}.meanP",
                       k=k_values),
                "results/" + project + "/faststructure/" + project + ".faststructure.chooseK_results.txt",
                expand("results/" + project + "/faststructure/" + project + ".faststructure.K{k}.map.pdf",
                       k=k_values)
            ])

        # ADMIXTURE targets
        if analyses.get("admixture", False):
            targets.extend([
                expand("results/" + project + "/admixture/" + project + ".biallelic_snps_thinned.{k}.Q",
                       k=k_values),
                expand("results/" + project + "/admixture/" + project + ".biallelic_snps_thinned.{k}.P",
                       k=k_values),
                "results/" + project + "/admixture/" + project + ".admixture.chooseK_results.txt",
                expand("results/" + project + "/admixture/" + project + ".admixture.K{k}.map.pdf",
                       k=k_values)
            ])

        # STRUCTURE targets
        if analyses.get("structure", False):
            targets.extend([
                expand("results/" + project + "/structure/" + project + ".structure.K{k}.R{r}_f",
                       k=k_values,
                       r=range(1, n_reps + 1)),
                "results/" + project + "/structure/" + project + ".K_aligned.pdf",
                expand("results/" + project + "/structure/" + project + ".structure.K{k}.Qmatrix.txt",
                       k=k_values),
                expand("results/" + project + "/structure/" + project + ".structure.K{k}.map.pdf",
                       k=k_values)
            ])

        # PCAone targets (standard)
        if analyses.get("pcaone", False):
            targets.extend([
                "results/" + project + "/pcaone/" + project + ".PCA.eigvecs",
                "results/" + project + "/pcaone/" + project + ".PCA.eigvals",
                expand("results/{project}/pcaone/plots/{project}.PCA-{color_by}.pdf",
                    project=project,
                    color_by=PCA_groupings)
            ])

        # PCAone targets (EMU)
        if analyses.get("pcaone_emu", False):
            targets.extend([
                "results/" + project + "/pcaone_EMU/" + project + ".PCA_EMU.eigvecs",
                "results/" + project + "/pcaone_EMU/" + project + ".PCA_EMU.eigvals"
            ])

        # PCAone targets (with coverage thresholds)
        if analyses.get("pcaone_miss", False):
            miss_values = params.get("PCAone", {}).get("miss", [0.5])
            targets.extend([
                expand("results/" + project + "/pcaone_miss{miss}/" + project + ".PCA_miss{miss}.eigvecs",
                       miss=miss_values),
                expand("results/" + project + "/pcaone_miss{miss}/" + project + ".PCA_miss{miss}.eigvals",
                       miss=miss_values)
            ])
        
        # VCF2PCACluster targets
        if analyses.get("vcf2pcacluster", False):
            vcf2pca_params = params.get("vcf2pcacluster", {}).get("SNP_filtering", {})
            miss_values = vcf2pca_params.get("Miss", [0.5])
            maf_values = vcf2pca_params.get("MAF", [0])

            targets.extend(
                expand(
                    "results/" + project + "/vcf2pcacluster_miss{miss}_MAF{MAF}/" + project + ".vcf2pcacluster_miss{miss}_MAF{MAF}.eigenvec",
                    miss=miss_values,
                    MAF=maf_values
                )
            )
            targets.extend(
                expand(
                    "results/" + project + "/vcf2pcacluster_miss{miss}_MAF{MAF}/" + project + ".vcf2pcacluster_miss{miss}_MAF{MAF}.eigenval",
                    miss=miss_values,
                    MAF=maf_values
                )
            )

        # Pixy targets
        if analyses.get("pixy", False):
            targets.extend([
                "results/" + project + "/pixy/" + project + ".pixy_pi-summary.txt",
                "results/" + project + "/pixy/" + project + ".pixy_fst-summary.txt",
                "results/" + project + "/pixy/" + project + ".pixy_dxy-summary.txt"
            ])

        # Relatedness targets
        if analyses.get("relatedness", False):
            targets.extend([
                "results/" + project + "/relatedness/" + project + ".relatedness",
                "results/" + project + "/relatedness/" + project + ".relatedness2"
            ])
        
        # IQtree targets
        if analyses.get("iqtree", False):
            targets.extend([
                "results/" + project + "/iqtree/" + project + ".iqtree.treefile",
                "results/" + project + "/iqtree/" + project + ".iqtree.iqtree",
                "results/" + project + "/iqtree/" + project + ".iqtree.log",
                "results/" + project + "/iqtree/" + project + ".tree_plot.pdf",
                "results/" + project + "/iqtree/" + project + ".tree_plot.rds"
            ])

        # IQtree robust targets
        if analyses.get("iqtree_robust", False):
            targets.extend([
                "results/" + project + "/iqtree_robust/" + project + ".iqtree_robust.treefile",
                "results/" + project + "/iqtree_robust/" + project + ".iqtree_robust.iqtree",
                "results/" + project + "/iqtree_robust/" + project + ".iqtree_robust.log",
                "results/" + project + "/iqtree_robust/" + project + ".tree_plot.pdf",
                "results/" + project + "/iqtree_robust/" + project + ".tree_plot.rds"
            ])

    return targets

# Main target rule
rule all:
    input:
        get_all_targets()
